pkgbase=dxvk-git
pkgname=('dxvk-win64-git' 'dxvk-win32-git' 'dxvk-git')
pkgver=0.65_42_g861165f
pkgrel=1
pkgdesc="A Vulkan-based compatibility layer for Direct3D 10/11 which allows running 3D applications on Linux using Wine."
arch=('i686' 'x86_64')
url="https://github.com/doitsujin/dxvk"
license=('zlib/libpng')
makedepends=('ninja' 'meson>=0.43' 'glslang' 'git' 'wine' 'mingw-w64-gcc')
options=(!strip !buildflags staticlibs)
source=("git+https://github.com/doitsujin/dxvk.git"
        "setup_dxvk_aur"
        "setup_dxvk_aur.verb"
)
sha256sums=("SKIP"
            "b727c7d6b5ff8b96a85e114fc0a3c945c0c405e4c8a6d40a24db1f8aa10f8b01"
            "6595b87836e5da0f25d5be46cf2f9a84493e7b04fa1e5dbb11d110a240d2204e")

pkgver() {
    cd dxvk
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g'
}

build() {
    meson dxvk "build/x64" \
        --cross-file dxvk/build-win64.txt \
        --prefix "/usr/share/dxvk/x64" \
        --bindir "" --libdir "" \
        --buildtype "release" \
        --strip \
        -D enable_tests=false
    ninja -C "build/x64"

    meson dxvk "build/x32" \
        --cross-file dxvk/build-win32.txt \
        --prefix "/usr/share/dxvk/x32" \
        --bindir "" --libdir "" \
        --buildtype "release" \
        --strip \
        -D enable_tests=false
    ninja -C "build/x32"
}

_package_dxvk() {
    DESTDIR="$pkgdir" ninja -C "build/x$1" install
    install -Dm 644 setup_dxvk_aur.verb "$pkgdir/usr/share/dxvk/x$1/setup_dxvk_aur.verb"
    install -Dm 755 setup_dxvk_aur "$pkgdir/usr/bin/setup_dxvk$1"
    sed "s/_ARCH/$1/g" -i "$pkgdir/usr/share/dxvk/x$1/setup_dxvk_aur.verb"
    sed "s/_ARCH/$1/g" -i "$pkgdir/usr/bin/setup_dxvk$1"
}

package_dxvk-win64-git() {
    arch=('x86_64')
    depends=('vulkan-icd-loader' 'wine>=3.10' 'winetricks')
    provides=("dxvk" "dxvk64")
    conflicts=("dxvk64")

    _package_dxvk 64
}

package_dxvk-win32-git() {
    arch=('i686' 'x86_64')
    depends=('lib32-vulkan-icd-loader' 'wine>=3.10' 'winetricks')
    provides=("dxvk" "dxvk32")
    conflicts=("dxvk32")

    _package_dxvk 32
}

package_dxvk-git() {
    pkgdesc="Dummy package to smooth the transition to the split packages"
    depends=("dxvk-win32-git" "dxvk-win64-git")
    provides=("dxvk")
}
