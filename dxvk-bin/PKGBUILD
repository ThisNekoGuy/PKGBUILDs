pkgbase=dxvk-bin
pkgname=('dxvk-win64-bin' 'dxvk-win32-bin' 'dxvk-bin')
pkgver=0.94
pkgrel=1
pkgdesc="A Vulkan-based compatibility layer for Direct3D 10/11 which allows running 3D applications on Linux using Wine (binary files)"
arch=('i686' 'x86_64')
url="https://github.com/doitsujin/dxvk"
license=('zlib/libpng')
options=(!strip)
source=("https://github.com/doitsujin/dxvk/releases/download/v$pkgver/dxvk-$pkgver.tar.gz"
        "setup_dxvk_aur"
        "setup_dxvk_aur.verb"
)
sha256sums=("1f06bfac5b435b62b972806fb3bbd86f7ccae2399b4451e85ae414e03d3712a3"
            "b727c7d6b5ff8b96a85e114fc0a3c945c0c405e4c8a6d40a24db1f8aa10f8b01"
            "6595b87836e5da0f25d5be46cf2f9a84493e7b04fa1e5dbb11d110a240d2204e")

_package_dxvk() {
    install -dm 755 -p "$pkgdir/usr/"{bin,share/dxvk/x$1}
    cp -dr --no-preserve='ownership' "dxvk-$pkgver/x$1/"*.dll "$pkgdir/usr/share/dxvk/x$1/"
    install -m 644 setup_dxvk_aur.verb "$pkgdir/usr/share/dxvk/x$1/setup_dxvk_aur.verb"
    install -m 755 setup_dxvk_aur "$pkgdir/usr/bin/setup_dxvk$1"
    sed "s/_ARCH/$1/g" -i "$pkgdir/usr/share/dxvk/x$1/setup_dxvk_aur.verb"
    sed "s/_ARCH/$1/g" -i "$pkgdir/usr/bin/setup_dxvk$1"
}

package_dxvk-win64-bin () {
    arch=('x86_64')
    depends=('vulkan-icd-loader' 'wine>=3.10' 'winetricks')
    provides=("dxvk" "dxvk64")
    conflicts=("dxvk64")

    _package_dxvk 64
}

package_dxvk-win32-bin () {
    arch=('i686' 'x86_64')
    depends=('lib32-vulkan-icd-loader' 'wine>=3.10' 'winetricks')
    provides=("dxvk" "dxvk32")
    conflicts=("dxvk32")

    _package_dxvk 32
}

package_dxvk-bin () {
    pkgdesc="Dummy package to smooth the transition to the split packages"
    depends=("dxvk-win32-bin" "dxvk-win64-bin")
    provides=("dxvk")
}
