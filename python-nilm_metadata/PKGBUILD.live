
pkgbase=nilm_metadata
pkgname=(python-nilm_metadata python2-nilm_metadata)
pkgver=0.2.2.r2.gc7348dd
pkgrel=1
pkgdesc="A schema for modelling meters, measurements, appliances, buildings etc (nilmtk)"
arch=('any')
url="http://nilm-metadata.readthedocs.org/"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase"::'git+https://github.com/nilmtk/nilm_metadata.git')
md5sums=('SKIP')

pkgver() {
  cd "$pkgbase"
  printf "%s" "$(git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g')"
}

prepare() {
  cp -a $pkgbase{,-py2}

  cd "$pkgbase"-py2
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      $(find . -name '*.py')
}

package_python-nilm_metadata() {
  depends=('python' 'python-pyaml' 'python-dateutil' 'python-pytz')

  cd "$pkgbase"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-nilm_metadata() {
  depends=('python2' 'python2-pyaml' 'python2-dateutil' 'python2-pytz')

  cd "$pkgbase"-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
