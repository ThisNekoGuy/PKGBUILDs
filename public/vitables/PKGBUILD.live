# Maintainer: loathingkernel <username ta gmail tod com>
# Contributor: Miko≈Çaj Chwalisz <m.chwalisz@gmail.com>
# Contributor: archlinux.info:tdy

pkgname=vitables
pkgver=3.0.2.r14.g08709d4
pkgrel=1
pkgdesc="A GUI browser and editor for PyTables/HDF5 files"
arch=(any)
url=https://vitables.org
license=(GPL3)
depends=(python-{build,installer,wheel}
         python-setuptools-scm
  'python-pyqt5'
  'python-qtpy'
  'python-pytables'
)
makedepends=(
  'python-sphinx'
)
source=(
  "$pkgname::git+https://github.com/uvemas/ViTables.git"
  "vitables.desktop"
  "vitables.svg"
)

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$pkgname"
  python -m build -wn
}

package() {
  cd "$pkgname"
  python -m installer -d "$pkgdir" dist/*.whl
  install -d "$pkgdir"/usr/share/{applications,pixmaps,doc}
  install -m644 "$srcdir"/vitables.desktop "$pkgdir"/usr/share/applications/
  install -m644 "$srcdir"/vitables.svg "$pkgdir"/usr/share/pixmaps/
  mv "$pkgdir"/usr/'*' "$pkgdir"/usr/share/doc/"$pkgname"
}

sha256sums=('SKIP'
            'dd793f85d81591edd837a5fb94642dbf3ba488c69c605cbc2c87f74baea8676c'
            '0be6fdeb2c1f40fe2c478b0a6b9f99bad170d69f1210300d736bb98a5b9edcd8')
