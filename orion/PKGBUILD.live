# Maintainer: Giovanni Harting <539@idlegandalf.com>
# Contributor: David McInnis <davidm@eagles.ewu.edu>
# Contributor: HabarNam <habarnam@gmail.com>

pkgname=orion
pkgver=1.6.5.r5.gce0ac00
pkgrel=1
pkgdesc="QML/C++-written desktop client for Twitch.tv"
arch=('x86_64')
url="https://github.com/alamminsalo/orion/"
license=('GPL')
depends=('mpv' 'qt5-svg' 'qt5-quickcontrols2' 'qt5-quickcontrols' 'qt5-graphicaleffects')
makedepends=('git')
source=("git+https://github.com/alamminsalo/orion.git"
        "disable-mpv-hwdec.patch")
sha256sums=('SKIP'
            'd5e2658cecad38b5730010040c8ce3a9d930e423f57061958ef19271db58a30d')

pkgver() {
	cd "$srcdir/${pkgname}"
	printf "%s" "$(git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g')"
}

prepare() {
    cd "$srcdir/${pkgname}"
    patch -p1 -i $srcdir/disable-mpv-hwdec.patch
}

build() {
	cd "$srcdir/${pkgname}"
	qmake CONFIG+=mpv \
        QMAKE_CFLAGS_RELEASE="${CFLAGS}"\
        QMAKE_CXXFLAGS_RELEASE="${CXXFLAGS}"
	make
}

package() {
    cd "$srcdir/${pkgname}"

    install -Dm644 distfiles/orion.svg "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/orion.svg
    install -Dm644 distfiles/Orion.desktop "${pkgdir}"/usr/share/applications/Orion.desktop
    install -Dm755 orion "${pkgdir}"/usr/bin/orion
}
