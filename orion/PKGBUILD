# Maintainer: Giovanni Harting <539@idlegandalf.com>
# Contributor: David McInnis <davidm@eagles.ewu.edu>
# Contributor: HabarNam <habarnam@gmail.com>

pkgname=orion
pkgver=1.6.5
pkgrel=1
pkgdesc="QML/C++-written desktop client for Twitch.tv"
arch=('x86_64')
url="https://github.com/alamminsalo/orion/"
license=('GPL')
depends=('mpv' 'qt5-svg' 'qt5-quickcontrols2' 'qt5-quickcontrols' 'qt5-graphicaleffects')
source=("$pkgname-$pkgver"::"https://github.com/alamminsalo/orion/archive/${pkgver//_/\-}.tar.gz"
        "disable-mpv-hwdec.patch")
sha256sums=('6ec51b063b7025e7cc0aaf09e8a6f9943d0d0a44ba768d2bb2bbd0e6ee60f853'
            'd5e2658cecad38b5730010040c8ce3a9d930e423f57061958ef19271db58a30d')


prepare() {
    cd "$srcdir/${pkgname}-${pkgver//_/\-}"
    patch -p1 -i $srcdir/disable-mpv-hwdec.patch
}

build() {
	cd "$srcdir/${pkgname}-${pkgver//_/\-}"
	qmake CONFIG+=mpv \
        QMAKE_CFLAGS_RELEASE="${CFLAGS}"\
        QMAKE_CXXFLAGS_RELEASE="${CXXFLAGS}"
	make
}

package() {
    cd "$srcdir/${pkgname}-${pkgver//_/\-}"

    install -Dm644 distfiles/orion.svg "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/orion.svg
    install -Dm644 distfiles/Orion.desktop "${pkgdir}"/usr/share/applications/Orion.desktop
    install -Dm755 orion "${pkgdir}"/usr/bin/orion
}
