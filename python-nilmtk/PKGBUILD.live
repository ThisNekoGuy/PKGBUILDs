
pkgbase=nilmtk
pkgname=(python-nilmtk python2-nilmtk)
pkgver=0.3.alpha.r394.g218eb41
pkgrel=1
pkgdesc="Non-Intrusive Load Monitoring Toolkit (nilmtk)"
arch=('any')
url="http://nilmtk.github.io/"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase"::'git+https://github.com/nilmtk/nilmtk.git')
md5sums=('SKIP')

pkgver() {
  cd "$pkgbase"
  printf "%s" "$(git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g')"
}

prepare() {
  cp -a $pkgbase{,-py2}

  cd "$pkgbase"-py2
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      $(find . -name '*.py')
}

package_python-nilmtk() {
  depends=('python' 'python-nilm_metadata' 'cython' 'python-bottleneck' 'python-numpy'
           'python-numexpr' 'python-pytables' 'python-pandas' 'python-matplotlib'
           'python-networkx' 'python-scipy' 'python-scikit-learn' 'python-jupyter_core'
           'ipython' 'python-ipykernel' 'python-six' 'python-future'
           'python-nose' 'python-coverage' 'python-pip' 'python-psycopg2' 'python-hmmlearn')

  cd "$pkgbase"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-nilmtk() {
  depends=('python2' 'python2-nilm_metadata' 'cython2' 'python2-bottleneck' 'python2-numpy'
           'python2-numexpr' 'python2-pytables' 'python2-pandas' 'python2-matplotlib'
           'python2-networkx' 'python2-scipy' 'python2-scikit-learn' 'python2-jupyter_core'
           'ipython2' 'python2-ipykernel' 'python2-six' 'python2-future'
           'python2-nose' 'python2-coverage' 'python2-pip' 'python2-psycopg2' 'python2-hmmlearn')

  cd "$pkgbase"-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
