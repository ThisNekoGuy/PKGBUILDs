# Maintainer: Major <majorx234@googlemail.com>
pkgname=python-inputs
_pyname=${pkgname#python-}
pkgver=0.5
pkgrel=1
pkgdesc="Cross-platform Python support for keyboards, mice and gamepads"
arch=('i686' 'x86_64' 'armv7h')
url="https://github.com/zeth/$_pyname"
license=(BSD)
makedepends=(python-{build,installer,wheel}
             python-setuptools-scm)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz"
        3203c9e25f1e14c4316d85d59c536b4e407f569f.patch)

prepare() {
    cd "$_archive"
    patch -p1 -i "$srcdir"/3203c9e25f1e14c4316d85d59c536b4e407f569f.patch
}

build() {
    cd "$_archive"
    python -m build -wn
}

package() {
    cd "$_archive"
    python -m installer -d "$pkgdir" dist/*.whl
    # Install LICENSE file
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    # Install README file
    install -Dm644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

sha256sums=('a31d5b96a3525f1232f326be9e7ce8ccaf873c6b1fb84d9f3c9bc3d79b23eae4')
sha256sums=('a31d5b96a3525f1232f326be9e7ce8ccaf873c6b1fb84d9f3c9bc3d79b23eae4'
            '92c69faff3038a42eb0fc4dfefe2657dea1e5cf0cea05c14d02ba04decdcc139')
